# SPDX-FileCopyrightText: 2021-2026 Zextras <https://www.zextras.com>
#
# SPDX-License-Identifier: AGPL-3.0-only

# Please refer to the table below to check envoy and consul
# compatibility:
#   - https://www.consul.io/docs/connect/proxies/envoy

pkgname="service-discover-base"
pkgver="1.21.4"
pkgrel="2"
pkgdesc="Service discover binary, based on HashiCorp Consul"
maintainer="Zextras <packages@zextras.com>"
arch=('x86_64')
license=("MPL-2.0")
section="admin"
priority="optional"
url="https://www.zextras.com/"
conflicts=("consul")
source=(
  "https://github.com/hashicorp/consul/archive/v${pkgver}.tar.gz"
  "systemd-sysuser.conf"
)

sha256sums=(
  'f6c5a2a67c422b447afc91b3eafbf5740ca26b5c85aac1832a23fb737807f6a0'
  'ee3f2413084b52f5cfa310ce5e1311d86283848e5405cad992365dfea1db80ce'
)

build() {
  cd "${srcdir}/consul-${pkgver}"

  # consul is incompatible with -buildmode=pie
  export GOFLAGS="-trimpath -mod=readonly -modcacherw"
  CGO_ENABLED=0 \
    go build -ldflags="-s -w"
}

package() {
  cd "${srcdir}/consul-${pkgver}"

  install -Dm755 consul \
    "${pkgdir}/usr/bin/consul"

  # systemd sysusers.d
  install -Dm644 "${srcdir}/systemd-sysuser.conf" \
    "${pkgdir}/usr/lib/sysusers.d/service-discover.conf"
}

postinst() {
  systemd-sysusers /usr/lib/sysusers.d/service-discover.conf >/dev/null 2>&1 || :
}
